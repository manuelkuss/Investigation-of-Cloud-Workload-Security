let storageAccountsToWatch = dynamic(["arttest7cafd9stacc2", "arttest7cafd9fappmaaee3"]);
let storageFileLogsEvents = StorageFileLogs 
    | where OperationName == "CreateFile"
    | where _ResourceId has_any(storageAccountsToWatch)
    | extend StorageFileLogs_JoinAttrResourceId = _ResourceId
    | extend StorageFileLogs_CallerIpAddress = tostring(split(CallerIpAddress, ":", 0)[0])
    | extend StorageFileLogs_TimeGenerated = TimeGenerated
    | project
        StorageFileLogs_TimeGenerated,
        StorageFileLogs_JoinAttrResourceId,
        StorageFileLogs_CallerIpAddress;
let azureActivityEvents = AzureActivity
    | where OperationNameValue == "MICROSOFT.STORAGE/STORAGEACCOUNTS/LISTKEYS/ACTION"
    | extend Properties_json = parse_json(Properties)
    | extend AzureActivity_JoinAttrResourceId = tostring(Properties_json.entity)
    | where AzureActivity_JoinAttrResourceId has_any (storageAccountsToWatch)
    | extend AzureActivity_CallerIpAddress = CallerIpAddress
    | extend AzureActivity_TimeGenerated = TimeGenerated
    | extend Authorization_json = parse_json(Authorization)
    | extend Authorization_evidence_json = parse_json(Authorization_json.evidence)
    | extend Authorization_role = Authorization_evidence_json.role
    | project
        AzureActivity_TimeGenerated,
        AzureActivity_JoinAttrResourceId,
        AzureActivity_CallerIpAddress,
        Caller,
        Authorization_role;
storageFileLogsEvents
    | join kind=inner (azureActivityEvents) on $left.StorageFileLogs_CallerIpAddress == $right.AzureActivity_CallerIpAddress
    | where StorageFileLogs_JoinAttrResourceId has AzureActivity_JoinAttrResourceId
    | where StorageFileLogs_TimeGenerated > AzureActivity_TimeGenerated
    | where StorageFileLogs_TimeGenerated < datetime_add('minute', 1, AzureActivity_TimeGenerated)
    | where Authorization_role !in~ ("Contributor", "Owner")
    | summarize AzureActivity_TimeGenerated = arg_max(AzureActivity_TimeGenerated, *) by StorageFileLogs_TimeGenerated